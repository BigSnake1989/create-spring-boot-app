/**
 * @author 王下邀月熊
 */

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "gradle.plugin.com.arenagod.gradle:mybatis-generator-plugin:1.3"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE"
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.7"
        classpath "org.springframework:springloaded:1.2.6.RELEASE"
    }
}

ext {
    // 设置脚本地址
    gradleScriptDir = "${rootProject.projectDir}/gradle"

    // 设置版本号
    springBootVersion = "1.5.2.RELEASE"
}



allprojects {

    // 插件
    apply plugin: 'java'
    apply plugin: "spring-boot"
    apply plugin: "com.arenagod.gradle.MybatisGenerator"
    apply plugin: "idea"


    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("${buildDir}/classes/main/")
        }
    }

    // 仓库
    repositories {

        // 引入本地依赖
        maven { url "file:" + rootProject.projectDir + "/lib/" }
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/snapshot' }
        maven { url 'http://repo.spring.io/milestone' }
        maven { url 'https://jitpack.io' }
        maven { url 'http://repo.maven.apache.org/maven2' }

    }

    // 设置组名
    group = "wx.csba"

    // 设置源代码路径
    sourceSets {
        main {
            java {
                srcDir "src/main/java"
            }
            resources {
                srcDir "src/main/resources"
                srcDir "src/main/java"
                exclude "**/*.java"
            }
        }
    }

    // 编译兼容及目标版本
    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options*.compilerArgs = [
                // 编译参数
                // "-Xlint:deprecation",
                "-Werror"
        ]
    }

    // 测试兼容及目标版本
    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options*.compilerArgs = [
                // 测试编译参数
                "-parameters"
        ]
    }

    // 依赖
    dependencies {

        // 通用依赖
        compile group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.0-b01'
        compile group: 'commons-io', name: 'commons-io', version: '2.4'
        compile group: 'commons-lang', name: 'commons-lang', version: '2.6'

        // 数据处理
        compile "org.projectlombok:lombok:1.16.8"
        compile group: "com.alibaba", name: "fastjson", version: "1.2.14"

        // Spring Boot
        compile(group: "org.springframework.boot", name: "spring-boot-starter-web", version: springBootVersion) {
            exclude(module: 'spring-boot-starter-tomcat')
            exclude group: "commons-logging", module: "commons-logging"
        }
        compile "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
        compile group: "org.springframework.boot", name: "spring-boot-starter-actuator", version: springBootVersion
        compile group: "org.springframework.boot", name: "spring-boot-starter-jetty", version: springBootVersion
        compile group: "org.springframework.boot", name: "spring-boot-starter-jdbc", version: springBootVersion
        compile group: "org.springframework.boot", name: "spring-boot-starter-aop", version: springBootVersion

        // 日志记录
        compile 'org.slf4j:slf4j-api:1.7.21'

        // 测试驱动
        testCompile 'junit:junit'
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}

dependencies {
    compile project(':module:api')
    compile project(':module:model')
}

// 设置 Spring Boot 根类
bootRepackage {
    mainClass = 'wx.csba.webapp.Application'
}

// 设置打包生成的 Jar 包信息
jar {
    baseName = 'spring-boot-app-boilerplate'
    version = '0.0.1-SNAPSHOT'
}

// api 模块
project(':module:api') {
    dependencies {
        compile project(':module:model')
    }
}

project(':module:model') {

}

// 辅助程序

task help {

    doLast {
        println "使用 gradle :booRun 运行 Spring Boot 项目（这里的 : 表示从根模块开始运行）"
        println "使用 gradle :build 打包 Jar"
        println "使用 gradle task 查看所有任务"
    }

}

